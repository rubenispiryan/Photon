include "macos.phtn"

// Standard Type Definitions

// struct Str {
//   length: uint64 (8 bytes)
//   data: char* (8 bytes)
// }

// Standard Constant Definitions

macro NULL 0 end
macro true 0 0 == end
macro false 0 0 != end

// IO Buffers
macro stdin 0 end
macro stdout 1 end
macro stderr 2 end

// mmap args
macro MAP_PRIVATE 2 end
macro PROT_READ 1 end

// type sizes
macro sizeof(ptr)       8 end
macro sizeof(u64)       8 end
macro sizeof(u32)       4 end
macro sizeof(u16)       2 end
macro sizeof(timespec) 16 end

// struct stat of fstat64
macro sizeof(stat) 144 end
macro stat.st_dev 0 + end
macro stat.st_mode 4 + end
macro stat.st_nlink 6 + end
macro stat.st_ino 8 + end
macro stat.st_uid 16 + end
macro stat.st_gid 20 + end
macro stat.st_rdev 24 + end
macro stat.st_atimespec 32 + end
macro stat.st_mtimespec 48 + end
macro stat.st_ctimespec 64 + end
macro stat.st_size 96 + end
macro stat.st_blocks 104 + end
macro stat.st_blksize 112 + end
macro stat.st_flags 116 + end
macro stat.st_gen 120 + end
macro stat.st_lspare 124 + end
macro stat.st_qspare 128 + end
macro sizeof(st_dev) sizeof(u32) end
macro sizeof(st_mode) sizeof(u16) end
macro sizeof(st_nlink) sizeof(u16) end
macro sizeof(st_ino) sizeof(u64) end
macro sizeof(st_uid) sizeof(u32) end
macro sizeof(st_gid) sizeof(u32) end
macro sizeof(st_rdev) sizeof(u32) end
macro sizeof(st_atimespec) sizeof(timespec) end
macro sizeof(st_mtimespec) sizeof(timespec) end
macro sizeof(st_ctimespec) sizeof(timespec) end
macro sizeof(st_size) sizeof(u64) end
macro sizeof(st_blocks) sizeof(u64) end
macro sizeof(st_blksize) sizeof(u32) end
macro sizeof(st_flags) sizeof(u32) end
macro sizeof(st_gen) sizeof(u32) end
macro sizeof(st_lspare) sizeof(u32) end
macro sizeof(st_qspare) sizeof(u64) 2 * end

// Str constants
macro sizeof(Str) 16 end
macro Str.length 0 + end
macro Str.data 8 + end

// putd proc constant
macro PUTD_BUFFER_CAP 32 end

// Standard Procedure Definitions

// stack operations
proc dup2 over over end
proc drop2 drop drop end

// math operations
proc % dup2 / * - end

// syscall wrappers
proc write SYS_WRITE syscall3 end
proc read SYS_READ syscall3 end
proc exit SYS_EXIT syscall1 drop end
proc open SYS_OPEN syscall2 end
proc close SYS_CLOSE syscall1 end
proc fstat64 SYS_FSTAT64 syscall2 end
proc mmap SYS_MMAP syscall6 end

// write syscall wrappers
proc puts stdout write drop end
proc putsln puts "\n" puts end
proc eputs stderr write drop end
proc eputsln eputs "\n" eputs end

// memory manipulations
proc inc8 dup @8 1 + swap !8 end
proc dec8 dup @8 1 - swap !8 end

proc not
    ->int bnot 1 and ->bool
end

proc isdigit
    '0' >=
    over '9' <=
    and
end

// Str procs
proc !Str.length Str.length !8 end
proc !Str.data Str.data !8 end
proc @Str.length Str.length @8 end
proc @Str.data Str.data @8 ->ptr end

proc @Str // str: Str -> int ptr
    dup @Str.length swap @Str.data
end

proc !Str // n: int, s: ptr, str: Str -> None
    rot over
    !Str.length
    !Str.data
end

// Remove left most character from s
proc Str.chop() // s: Str -> None
    dup Str.length dec8
    Str.data inc8
end

proc Str.chop_line() // line: Str input: Str -> None
    dup2 Str.data @8 swap Str.data !8
    over Str.length 0 swap !8
    while
        if dup Str.length @8 0 > do
            dup Str.data @8 ->ptr @ '\n' !=
        else
            false
        end
    do
        dup Str.chop()
        over Str.length inc8
    end
    if dup Str.length @8 0 > do
        dup Str.chop()
    end
    drop2
end

proc Str.chop_word() // word: Str input: Str -> None
    dup2 Str.data @8 swap Str.data !8
    over Str.length 0 swap !8
    while
        if dup Str.length @8 0 > do
            dup Str.data @8 ->ptr @ ' ' !=
        else
            false
        end
    do
        dup Str.chop()
        over Str.length inc8
    end
    if dup Str.length @8 0 > do
        dup Str.chop()
    end
    drop2
end

proc Str.left_strip() // input: Str -> None
    while
        if dup Str.length @8 0 > do
            dup Str.data @8 ->ptr @ ' ' ==
        else
            false
        end
    do
        dup Str.chop()
    end drop
end

memory streq_a sizeof(Str) end
memory streq_b sizeof(Str) end
proc Str.equal() // n1: int, s1: ptr, n2: int, s2: ptr -> bool
    streq_a !Str
    streq_b !Str
    if streq_a @Str.length streq_b @Str.length == do
        0 while
            if dup streq_a @Str.length < do
                streq_a @Str.data @ streq_b @Str.data @ ==
            else
                false
            end
        do
            1 +
        end
        streq_a @Str.length ==
    else
        false
    end
end

memory parse_int_var sizeof(Str) end
proc Str.parse_int() // n: int, s: ptr -> int
    parse_int_var !Str
    0 0 while dup parse_int_var @Str.length < do
        dup parse_int_var @Str.data + @
        if dup isdigit not do
            // TODO: Fix error reporting for photon.phtn
            // report_error "Unknown word: " eputs word @Str eputsln
            "Error: Unknown word" eputs
            1 exit
        end
        '0' - rot +
        swap
        1 +
    end drop
end

proc strlen
   dup
   while dup @ 0 != do 1 + end
   swap -
end

proc streq // str1: ptr str2: ptr -> bool
    while
        if over @ 0 != over @ 0 != and do
            dup2 @ swap @ ==
        else
            false
        end
    do
        1 + swap 1 +
    end
    @ 0 == swap @ 0 == and
end

proc argv[n]
    8 * argv + @8 ->ptr
end

memory putd_buffer PUTD_BUFFER_CAP end
// puts int into stdout
proc putd
  if dup 0 == do
    "0" puts
  else
    if dup 0 < do
      "-" puts
      0 swap -
    end
    putd_buffer PUTD_BUFFER_CAP +
    while over 0 > do
      1 - dup rot
      10 dup2 / rot rot %
      rot swap '0' + swap ! swap
    end
    dup
    putd_buffer PUTD_BUFFER_CAP + swap - swap puts
  end
  drop
end

// puts int into stderr
proc eputd
  if dup 0 == do
    "0" eputs
  else
    if dup 0 < do
      "-" eputs
      0 swap -
    end
    putd_buffer PUTD_BUFFER_CAP +
    while over 0 > do
      1 - dup rot
      10 dup2 / rot rot %
      rot swap '0' + swap ! swap
    end
    dup
    putd_buffer PUTD_BUFFER_CAP + swap - swap eputs
  end
  drop
end