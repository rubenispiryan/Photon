// Solution for: https://projecteuler.net/problem=5

include "std.phtn"

macro N 20 endmacro

// |acc          |tmp           |ans|
macro acc mem endmacro
macro tmp mem 8 N * + endmacro
macro ans tmp 8 N * + endmacro

2 while dup N <= do
  // clean up the tmp table
  0 while dup N < do
    tmp over 8 * + 0 .64
    1 +
  end drop

  // factorize and store factor count in tmp table
  dup 2 while over 1 > do
    dup2 % 0 == if
      dup 8 * tmp + inc64
      swap over / swap
    else
      1 +
    end
  end drop2

  // acc[i] = max(tmp[i], acc[i])
  0 while dup N < do
    dup 8 * acc + ,64
    over 8 * tmp + ,64
    < if
      dup 8 * acc +
      over 8 * tmp + ,64
      .64
    end
    1 +
  end drop
  1 +
end drop

ans 1 .64

// read the acc table and multiply into ans
0 while dup N < do
  acc over 8 * + ,64 while dup 0 > do
    over ans ,64 * ans swap .64
    1 -
  end drop
  1 +
end drop


ans ,64 print