// Solution for: https://projecteuler.net/problem=5

include "std.phtn"

macro N 20 end

// |acc          |tmp           |ans|
memory acc 8 N * end
memory tmp 8 N * end
memory ans sizeof(u64) end

2 while dup N <= do
  // clean up the tmp table
  0 while dup N < do
    tmp over 8 * + 0 swap !8
    1 +
  end drop

  // factorize and store factor count in tmp table
  dup 2 while over 1 > do
    if dup2 % 0 == do
      dup 8 * tmp + inc8
      swap over / swap
    else
      1 +
    end
  end drop2

  // acc[i] = max(tmp[i], acc[i])
  0 while dup N < do
    if  dup 8 * acc + @8
        over 8 * tmp + @8
        < do
        dup 8 * acc +
        over 8 * tmp + @8
        swap !8
    end
    1 +
  end drop
  1 +
end drop

ans 1 swap !8

// read the acc table and multiply into ans
0 while dup N < do
  acc over 8 * + @8 while dup 0 > do
    over ans @8 * ans !8
    1 -
  end drop
  1 +
end drop


ans @8 print