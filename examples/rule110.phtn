// Implementation of Rule 110 cellular automata
// Shows the turing completeness of Photon

include "std.phtn"

// Width of the row
macro N 100 endmacro

// Memory layout
macro row mem endmacro
macro display row N + endmacro

row N 2 - + 1 .
display N + 10 .

0 while dup N 2 - < do
   0 while dup N < do
      dup row + , if
         dup display + '*' .
      else
         dup display + ' ' .
      end
      1 +
   end drop

   N 1 + display write

   row     , 1 <<
   row 1 + ,
   |

   1 while dup N 2 - < do
      swap 1 << 7 &
      over row + 1 + , |
      dup2 110 swap >> 1 &
      swap row + swap .
      swap

      1 +
   end drop2

   1 +
end drop