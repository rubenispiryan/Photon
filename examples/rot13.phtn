// Implements ROT13
// (ROT13 is a substitution cipher where each letter is shifted by 13)

include "std.phtn"

macro BUFFER_CAP 1024 end

macro buffer mem end

// str_ptr shift_amount
macro shift_letter_by_n
    over , dup 'a' >= swap 'z' <= and if
        dup2 over , + 'a' - 26 % 'a' + .
    end

    over , dup 'A' >= swap 'Z' <= and if
        dup2 over , + 'A' - 26 % 'A' + .
    end

    drop2
end


while BUFFER_CAP buffer stdin read dup 0 > do
    0 while dup2 > do
        dup buffer + dup , '\n' != if
            13 shift_letter_by_n
        else
            drop
        end
        1 +
    end drop
    buffer puts
end drop
