// Implements ROT13
// (ROT13 is a substitution cipher where each letter is shifted by 13)

include "std.phtn"

const BUFFER_CAP 1024 end

memory buffer BUFFER_CAP end

// str_ptr shift_amount
proc shift_letter_by_n
    if over @ dup 'a' >= swap 'z' <= and do
        dup2 over @ + 'a' - 26 % 'a' + swap !
    end

    if over @ dup 'A' >= swap 'Z' <= and do
        dup2 over @ + 'A' - 26 % 'A' + swap !
    end

    drop2
end


while BUFFER_CAP buffer stdin read dup 0 > do
    0 while dup2 > do
        dup buffer +
        if dup @ '\n' != do
            13 shift_letter_by_n
        else
            drop
        end
        1 +
    end drop
    buffer puts
end drop
