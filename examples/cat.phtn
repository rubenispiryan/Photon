// This is an implementation of the UNIX cat command in Photon

include "std.phtn"

const BUFFER_CAP 1024 end

// memory layout
memory fd sizeof(ptr) end
memory buffer_count sizeof(u64) end
memory argn sizeof(u64) end
memory buffer BUFFER_CAP end

// remove count from string literal
proc only_str swap drop end

// assumes that a file descriptor is stored at fd to be read from
proc write_fd
    while BUFFER_CAP buffer fd @8 read dup 0 > do
        buffer puts
    end drop
end

// opens a file descriptor from a filename given as the n-th argument
// saves it at fd
proc open_fd_from_argv[n]
    O_RDONLY argn @8 argv[n] open
    if dup -1 == do
        "An error occurred when trying to open the file" putsln
        drop 1 exit
    else
        fd !8
    end
end

proc close_fd
    if fd @8 close 0 != do
        "An error occurred when trying to close the file" putsln
        1 exit
    end
end

// if no argument is provided enters interactive mode
// otherwise outputs all the files
if argc 2 < do
    fd stdin swap !8
    write_fd
else
    1 while dup argc < do
        dup argn !8
        open_fd_from_argv[n]
        write_fd
        close_fd
        1 +
    end drop
end